"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
load("@bazel_tools//tools/build_defs/repo:utils.bzl", "maybe")
load("//terser/private:versions.bzl", "TOOL_VERSIONS")

# WARNING: any changes in this function may be BREAKING CHANGES for users
# because we'll fetch a dependency which may be different from one that
# they were previously fetching later in their WORKSPACE setup, and now
# ours took precedence. Such breakages are challenging for users, so any
# changes in this function should be marked as BREAKING in the commit message
# and released only in semver majors.
def rules_terser_dependencies():
    # The minimal version of bazel_skylib we require
    maybe(
        http_archive,
        name = "bazel_skylib",
        sha256 = "c6966ec828da198c5d9adbaa94c05e3a1c7f21bd012a0b29ba8ddbccb2c93b0d",
        urls = [
            "https://github.com/bazelbuild/bazel-skylib/releases/download/1.1.1/bazel-skylib-1.1.1.tar.gz",
            "https://mirror.bazel.build/github.com/bazelbuild/bazel-skylib/releases/download/1.1.1/bazel-skylib-1.1.1.tar.gz",
        ],
    )
    maybe(
        http_archive,
        name = "aspect_rules_js",
        sha256 = "3e64e87a7885f1f4ae21ffaa2dc512b9bc315ff8b6e6332c9ccd5b38d66e230b",
        strip_prefix = "rules_js-0.4.0",
        url = "https://github.com/aspect-build/rules_js/archive/refs/tags/v0.4.0.tar.gz",
    )

########
# Remaining content of the file is only used to support toolchains.
########
_DOC = "Fetch external tools needed for terser toolchain"
_ATTRS = {
    "tool": attr.label(executable = True, cfg = "exec"),
}

def _terser_repo_impl(repository_ctx):
    repository_ctx.symlink(Label("//terser/private:run_terser.js"), "run_terser.js")

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", """\
# Generated by aspect_rules_terser/terser/repositories.bzl
load("@aspect_rules_js//js:nodejs_binary.bzl", "nodejs_binary")

nodejs_binary(
    name = "{name}",
    data = ["{tool}"],
    entry_point = "run_terser.js",
    visibility = ["//visibility:public"],
)
""".format(
        name = repository_ctx.attr.name,
        tool = repository_ctx.attr.tool,
    ))

terser_repositories = repository_rule(
    _terser_repo_impl,
    doc = _DOC,
    attrs = _ATTRS,
)

# Wrapper macro around everything above, this is the primary API
def terser_register_toolchains(name, terser_version, **kwargs):
    """Convenience macro for users which does typical setup.

    Users can avoid this macro and do these steps themselves, if they want more control.
    Args:
        name: base name for all created repos, like "terser"
        terser_version: a version which is mirrored into rules_terser
        **kwargs: passed to each node_repositories call
    """
    if terser_version not in TOOL_VERSIONS.keys():
        fail("""\
terser version {} is not currently mirrored into rules_terser.
Please instead choose one of these available versions: {}
Or, make a PR to the repo running /scripts/mirror_release.sh to add the newest version.
If you need custom versions, please file an issue.""".format(terser_version, TOOL_VERSIONS.keys()))

    TOOL_VERSIONS[terser_version]()

    terser_repositories(
        name = name,
        tool = "@npm_terser-{0}//:npm_terser-{0}".format(terser_version.lstrip("v")),
    )
